;====================================================================
; A reversible affine model for spacer regions between binding sites,
; with different models for short indels (1-2bp) and long indels (>2)
; W.H. Majoros
;====================================================================

; eT(x,t) = e^(-x*t)
(define 'eT [x t| (exp (- 0 (* x t))) ] )

; - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

; emT(x,t) = 1-e^(-x*t)
(define 'emT [x t| (- 1 (eT x t)) ] )

; - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

; bINF = lim of b(t) as t->inf
;   lambda = instantaneous birth rate
;   mu = instantaneous death rate
(define 'bINF [lambda mu|(/ (float lambda) (+ lambda mu))]) 
;(define 'bINF [|(float density)])

; - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

; bT(t) = binding-site "birth" probability over interval t
;   t = interval length
;   lambda = instantaneous birth rate
;   mu = instantaneous death rate
(define 'bT
  [t lambda mu|
    (* (bINF lambda mu) 
      (emT (+ lambda mu) t)
    )
  ]
)

; - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

; stop(t) = probability of going to stop state
(define 'stop 
  [t lambda mu|
    (- 1 (* (- 1 (bINF lambda mu)) (- 1 (bT t lambda mu))))
  ]
)

; - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

; nonstop(t,x) = x*(1-stop(t))
(define 'nonstop
  [t lambda mu x|
    (* x (- 1 (stop t lambda mu)))
  ] 
)

; nonstopG(t,x,gamma) = gamma*x*(1-stop(t))
(define 'nonstopG
  [t lambda mu x gamma|
    (* gamma (* x (- 1 (stop t lambda mu))))
  ] 
)

; - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

; short(t,L,m,a,b,g) = (1-g)(1-stop(t))(1-exp(-a*t))
(define 'short
  [t lambda mu alpha beta gamma|
    (* (- 1 gamma) (* (emT alpha t) (- 1 (stop t lambda mu))))
  ]
)

;====================================================================
; short-indel(alpha,beta,gamma,L,mu,fc) builds the reversible affine model:
;   alpha = tendency to stay in match state
;   beta = tendency to stay in insert/delete state
;   gamma = tendency to use long/short indel model
;   L ("lambda") = instantaneous birth rate for binding sites
;   mu = intantaneous death rate for binding sites
;   fc = functional class (background)
(define 'short-indel
  [alpha beta gamma L mu fc| ; model parameters
    ([M I D SHORT I1 I2 D1 D2| ; states
       (transducer
         (states M I D SHORT I1 I2 D1 D2)
         (transition start-state I [t|(/ (emT alpha t) 2)])
         (transition start-state D [t|(/ (emT alpha t) 2)])
         (transition start-state M [t|(eT alpha t)])
         (transition M M [t|(nonstop t L mu (eT alpha t))])
         (transition M SHORT [t|(short t L mu alpha beta gamma)])
         (transition SHORT I1 [t|0.25])
         (transition SHORT I2 [t|0.25])
         (transition SHORT D1 [t|0.25])
         (transition SHORT D2 [t|0.25])
         (transition I1 I2 [t|1])
         (transition I2 M [t|1])
         (transition D1 D2 [t|1])
         (transition D2 M [t|1])
         (transition M I [t|(nonstopG t L mu (/ (emT alpha t) 2) gamma)])
         (transition M D [t|(nonstopG t L mu (/ (emT alpha t) 2) gamma)])
         (transition M end-state [t|(stop t L mu)])
         (transition I I [t|(nonstop t L mu (emT beta t))])
         (transition I M [t|(nonstop t L mu (* (eT beta t) (eT alpha t)))])
         (transition I D [t|(nonstop t L mu (* (eT beta t) (emT alpha t)))])
         (transition I end-state [t|(stop t L mu)])
         (transition D D [t|(nonstop t L mu (emT beta t))])
         (transition D M [t|(nonstop t L mu (* (eT beta t) (eT alpha t)))])
         (transition D I [t|(nonstop t L mu (* (eT beta t) (emT alpha t)))])
         (transition D end-state [t|(stop t L mu)])
       )
     ]
     (state 1 MATCH fc) ; M
     (state 2 INSERT fc) ; I
     (state 3 DELETE fc) ; D
     (state 4 SILENT fc) ; SHORT
     (state 5 INSERT fc) ; I1
     (state 6 INSERT fc) ; I2
     (state 7 DELETE fc) ; D1
     (state 8 DELETE fc) ; D2
    )
  ]
)

